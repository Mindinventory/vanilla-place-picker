apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.Mindinventory'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 13
        versionName "0.1.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.10'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    testImplementation 'junit:junit:4.13'

    // Location
    api 'com.google.android.gms:play-services-location:17.1.0'
    api 'com.google.android.gms:play-services-maps:17.0.0'

    // this library are used for Retrofit API calling feature
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

    // Google material
    implementation 'com.google.android.material:material:1.2.1'

    implementation "com.google.android.libraries.places:places:2.4.0"

    //unittest
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'org.mockito:mockito-inline:2.19.0'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.5'
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
}
repositories {
    mavenCentral()
}

// Gradle task to check Google API key availability in AndroidManifest of root project
task validateManifest() {

    def xmlSource = file("${rootDir}/app/src/main/AndroidManifest.xml")
    def xmlParser = new XmlParser()
    def xmlRoot = xmlParser.parse(xmlSource)
    def metaDataNode = xmlRoot.application.'meta-data'
    def isAPIKeyAvailable = false
    metaDataNode.any { metadata ->

        metadata.attributes().any {

            if (it.value == "com.google.android.geo.API_KEY") {
                isAPIKeyAvailable = true
                return true
            } else isAPIKeyAvailable = false
        }

        if (isAPIKeyAvailable)
            return true
    }

    if (!isAPIKeyAvailable)
        throw new RuntimeException("Couldn\'t get Google API key from application meta data. Was it set in your AndroidManifest.xml?")
}
